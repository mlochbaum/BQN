# Tests of primitive functionality
# Ordering matches the reference implementations

# LAYER 0
0≡¯2+2
1e4≡5e3+5e3
'c'≡'a'+2
'a'≡¯2+'c'
! % 'a'+'c'
! % F←-⋄f+2
¯∞≡1e6-∞
4≡-¯4
¯∞≡-∞
∞≡-¯∞
4≡9-5
@≡'a'-97
3≡'d'-'a'
'Q'≡'q'+'A'-'a'
! % 97-'a'
! % @-1
! % -'a'
! % F←÷⋄-f
1.5≡3×0.5
! % 2×'a'
4≡÷0.25
∞≡÷0
0≡÷∞
! % ÷'b'
! % F←√-⋄÷f
1≡⋆0
¯1≡¯1⋆5
1≡¯1⋆¯6
! % ⋆'π'
! % 'e'⋆'π'
3≡⌊3.9
¯4≡⌊¯3.9
∞≡⌊∞
¯∞≡⌊¯∞
¯1e30≡⌊¯1e30
! % F←⌈⋄⌊f
1≡1=1
0≡¯1=∞
1≡'a'='a'
0≡'a'='A'
1≡{F←+⋄f=f}
1≡{a‿b←⟨+´,+´⟩⋄a=b}
0≡{_op←{𝕗}⋄op='o'}
0≡{F←{𝕩}⋄G←{𝕩}⋄f=g}
1≡{F←{𝕩}⋄f=f}
1≡1≤1
1≡¯∞≤¯1e3
0≡∞≤¯∞
1≡∞≤@
0≡'z'≤¯0.5
1≡'a'≤'a'
0≡'c'≤'a'
! % F←+⋄G←-⋄f≤g
⟨⟩≡≢<2
⟨3⟩≡≢"abc"
⟨2,3⟩≡≢>"abc"‿"fed"
⟨2,3,4,5⟩≡≢2‿3‿4‿5⥊↕120
⟨6⟩≡≢⥊>"abc"‿"fed"
"abc"≡0⊑"abc"‿"de"
"de"≡1⊑"abc"‿"de"
⟨⟩≡↕0
⟨0⟩≡↕1
⟨0,1,2,3,4,5,6⟩≡↕7
1≡!1
1≡'e'!1
! % !0
! % "error"!"abc"

# LAYER 1
3≡4>◶+‿-1
3≡4⊢◶+‿-1
3≡4 1◶+‿-1
5≡4<◶+‿-1
5≡4 0◶+‿-1
1≡-⊘0 ¯1
1≡¯1-⊘+2
"abc"≡⊢"abc"
""≡3⊢""
⟨⟩≡⊣⟨⟩
"ab"≡"ab"⊣⟨⟩
4≡+˜2
3≡1-˜4
1≡-∘×¯6
¯6≡2-∘×3
1≡-○×¯7
2≡5-○×¯7
¯20≡1⊸-⊸×5
(0‿2+⌜0‿1)≡(>⟨"ab","cd"⟩)≢⊸⥊↕4
20≡×⟜(-⟜1)5
4≡5+⟜×¯3
7≡5+⟜2 ¯3
2≡√4
3≡3√27
! % √'x'
6≡2∧3
0≡¯2∧0
! % 'a'∧¯1
0.75≡∨˜0.5
1.75≡2∨0.25
! % F←-⋄2∨f
0≡¬1
1≡¬0
2≡¬¯1
! % ¬'a'
0≡3¬4
2≡4¬3
4≡5¬2
5≡'g'¬'c'
'b'≡'c'¬2
! % 2¬'c'
! % F←{𝕩}⋄0¬f
0≡|0
5≡|¯5
6≡|6
∞≡|¯∞
! % F←+-⋄|f
2≡3|8
2≡3|¯7
¯1≡¯3|8
! % 26|'A'
"a"≡⥊<'a'
"abcd"≡⊑<"abcd"
⟨⟩≡≢<⟨2,⟨3,4⟩⟩
0≡4<2
0≡5>5
0≡3≥4
0≡≠""
1≡≠"a"
1≡≠'a'
2≡≠"ab"
25≡≠↕25
1≡×5
¯1≡×¯2.5
3≡3⌊4
¯3≡¯3⌊∞
4≡3⌈4
1≡1⌈¯1
5≡⌈4.01
⟨⟩≡≢'a'
⟨⟩≡≢0
⟨0⟩‿⟨1⟩‿⟨2⟩≡⥊¨↕3
(↕6)≡⟜(≠¨)○(2‿3⊸⥊)⟨⟩‿"a"‿"ab"‿"abc"‿"abcd"‿"abcde"‿"abcdef"
≡⟜(≠¨)4‿0‿2⥊↕0
6≡+´↕4
(⊑≡⊣´)"a"‿2‿(3‿"d")
0(⊑≡⊣´)"a"‿2‿(3‿"d")
(2⊸⊑≡⊢´)"a"‿2‿(3‿"d")
! % ⊑""
! % ⊑2‿0⥊⟨⟩
2(⊣≡⊢´)"a"‿2‿(3‿"d")
7‿10≡+¨´⟨⟨2,3⟩,⟨5,7⟩⟩
! % +´11
! % -´<'a'
! % ×´3‿1⥊"abc"

# LAYER 2
⟨⟩≡⟨⟩∾""
"a"≡⟨⟩∾"a"
"a"≡"a"∾⟨⟩
"aBCD"≡"a"∾"BCD"
((+⌜˜≠¨)≡(≠¨∾⌜˜))""‿⟨2,3⟩‿"abcde"
(⥊⟜(↕×´)≡(×⟜4)⊸(+⌜)○↕´)3‿4
(⥊⟜(↕×´)≡(×⟜4)⊸(+⌜)○↕´)0‿4
(3‿2‿0⥊"")≡(3‿2⥊↕6)+⌜""
(<-2)≡-¨2
(<<2)≡<¨2
⟨1,⟨3,2,2‿2⥊⟨1,0,2,0⟩⟩,⟨5,4⟩⟩≡-⟨-1,⟨-3,-2,-¨2‿2⥊⟨1,0,2,0⟩⟩,⟨-5,-4⟩⟩
3(+¨≡+⌜)↕6
! % 2‿3⊢¨4‿5‿6
! % "abcd"-"a"
3‿4‿5‿6‿6≡{𝕊⍟(×≡)⊸∾⟜⥊´𝕩}⟨2,1⟩+⟨⟨⟨⟨1,2⟩,3⟩,4⟩,5⟩
3‿2≡≢(↕3)(⊣×⊢⌜)↕2
(<-4)≡-<4
(<2)≡1+<1
! % (↕4)×(↕3)⊢⌜↕2
(=¨⟜(⥊⟜(↕×´)3‿4)≡(↕4)=⌜˜4|⊢)1‿6‿8
0‿1≡+‿-=⊑⟨-⟩

# LAYER 3
2≡⊑2
2≡⊑⟨2⟩
"ab"≡⊑⟨"ab"⟩
0≡⊑↕20
4≡⊑3‿2‿1⥊4⥊⊸∾5⥊0
'c'≡2⊑"abcd"
'c'≡¯2⊑"abcd"
7≡7⊑↕10
7≡⟨7⟩⊑↕10
0≡¯10⊑↕10
! % 10⊑↕10
! % ¯11⊑↕10
! % 0.5⊑↕10
! % 'x'⊑↕10
! % ⟨⟩⊑↕10
21≡2‿¯3⊑(10×↕3)+⌜↕4
! % 2⊑3+⌜○↕4
21‿12‿03≡⟨2‿¯3,1‿2,0‿¯1⟩⊑(10×↕3)+⌜↕4
! % 21‿12‿03≡⟨2‿¯3‿0,1‿2,0‿¯1⟩⊑(10×↕3)+⌜↕4
! % ⟨2,⟨3⟩⟩⊑↕4
! % (<2)⊑↕4
! % (≍≍2)⊑↕4
! % ⟨≍1‿2⟩⊑↕5‿5
"dfeb"≡(⥊¨-⟨3,1,2,5⟩)⊑"abcdef"
"abc"≡⟨⟩⊑<"abc"
'a'≡⟨⟩⊑'a'
⟨7,7‿7,7⟩≡⟨⟨⟩,⟨⟨⟩,⟨⟩⟩,⟨⟩⟩⊑<7
⟨7,⟨7,<7⟩⟩≡⟨⟨⟩,⟨⟨⟩,<⟨⟩⟩⟩⊑7
"abcfab"≡⥊(↕2‿3)⊑5‿5⥊"abcdef"
"aedcaf"≡⥊(-↕2‿3)⊑5‿5⥊"abcdef"
! % ↕@
! % ↕2.4
! % ↕<6
! % ↕≍2‿3
! % ↕¯1‿2
(<6⥊0)(⊑≡<∘⊑∘⊢)(6⥊1)⥊5
¯6≡1‿0◶(2‿2⥊0‿0‿-‿0)6
! % -˙◶÷‿× 4
⟨3⟩≡⥊3
(⟨⟩⊸⥊≡<)3
⟨3,3,3⟩≡3⥊3
⟨3,3,3⟩≡3<⊸⥊3
! % ¯3⥊3
! % 1.6‿2.5⥊↕4
! % (≍2‿3)⥊↕3
! % "     "≡5⥊""
6(⊢⌜≡∾○≢⥊⊢)○↕3
(<≡↕)⟨⟩
(↕∘⥊≡⥊¨∘↕)9
∧´(⟨∘⟩⊸⥊≡⥊)¨ ⟨4,↕4,↕2‿4⟩
! % 4‿∘⥊↕15
1‿2‿3‿0‿1≡⥊5‿⌽⥊↑‿4⥊3‿⌊⥊1+↕4
≡´⟨2‿⌽‿4,2‿3‿4⟩⥊¨<↕19
¬'a'≡<'a'
¬"a"≡≍"a"
¬⟨1,2,⟨4,4⟩,5⟩≡○(2‿2⊸⥊)⟨1,2,⟨3,4⟩,5⟩
¬2‿3‿4≡2‿3
¬1.001≡1.002
'a'≢2
2≢<2
2‿3≢2‿4
2‿3≢≍2‿3
0≡≡'a'
1≡≡↕6
2≡≡↕2‿4
3≡≡<<<4
(1¨≡-○≡˜⟜↕¨)⟨0,⟨⟩,⟨1⟩,2,⟨3,4⟩⟩
2≡≡⟨5,⟨'c',+,2⟩⟩
0≡≡⊑⟨-⟩

# LAYER 4
"a"≡⋈'a'
({⟨𝕩⟩}≡⋈)'a'‿2
"abc"‿1≡"abc"⋈1
⋈´⊸≡"ab"
∧´≡⟜>¨⟨1,<'a',<∞,↕5,5‿3⥊2⟩
2‿3‿2≡≢>↕2‿3
2‿3≡>⟨<2,3⟩
! % >↕¨2‿3
! % >⟨⥊2,3⟩
! % >(≍⋈⊢)↕4
((4⥊2)⊸⥊≡(>2‿2⥊·<2‿2⥊⊢))"abcd"
(⊢≡>∘<)5‿3⥊↕15
(⊢≡(><¨))5‿3⥊↕15
(⥊≡≍)'a'
(⥊≡≍)<'a'
(1‿2⊸⥊≡≍)"ab"
1‿2≡1≍2
2‿1(≍≡2‿2⥊∾)4‿3
(≍⟜<≡≍˜)'a'
! % 1‿0≍1‿2‿3
! % ≍⟜≍↕3
! % ⌽⎉1.1 ↕4
! % ⌽⎉'x' ↕4
! % ⌽⎉(<<0) ↕4
! % ⌽⎉≍ ↕4
(≍˘˜⥊˘1‿5‿9)≡⌽⎉2⊸+⥊⟜(↕×´)3‿2‿1
(<0)≡≡˘0
(<1)≡≡˘<0
(2⥊<<"ab") ≡ ⋈˜˘<"ab"
(3⥊0) ≡ {-}=˘↕3
(↕4)(×⌜≡×⎉0‿2)↕5
(↕4)(⋆˜⌜˜≡⋆⎉∞‿¯4)↕5
(⟨2⟩⊸∾⍟(2‿2⥊0‿1‿1‿1)2‿3)≡≢¨≍⎉(⌊○=)⌜˜⟨↕3,2‿3⥊↕6⟩
(2=⌜○↕3)≡(2‿4⥊"abc")≡⎉1(2‿3‿4⥊"abc")
⟨0,0⟩≡(2‿4⥊"abc")≡⎉¯1(2‿3‿4⥊"abc")
! % ⌽⚇2‿2.5 ↕3
(-≡-⚇¯1)5
⟨5,⟨15,1⟩⟩≡+´⚇1⟨⟨3,2⟩,⟨⟨4,5,6⟩,⟨1⟩⟩⟩
5‿6‿15≡∾´+´⚇1⟨⟨0,1⟩,⟨⟨⟩⟩⟩⥊⊸∾⚇¯2‿1⟨⟨2,3⟩,⟨4,5,6⟩⟩
(5⥊1)≡(↕5)=○=⚇0{≍}
! % 2+⍟1‿'c'4
! % ⋆⍟1.5 2
4≡2+⍟¯1 6
(2×↕7)≡2+⍟(¯3+↕7)6
(3⌊↕5)≡{i←0⋄r←{i+↩1⋄1+𝕩}⍟(↕4)𝕩⋄r∾i}0
(+⌜˜≡·>1+⍟⊢⊢)↕5
! % +˝6
! % ⊢˝<6
(+˝≡·<+´)¯8‿4‿¯38‿'!'‿21
(¯2⊸×≡·-˝×⌜˜)↕4
(⋈˝˘≡<¨)3‿2⥊"abcdef"
(⊣˝≡⊏)2‿1‿1‿2‿1⥊"abcd"
0‿1‿3‿6‿10≡+`↕5
(-0‿1‿3‿6‿10)≡-`↕5
((0∾¨↕3)≍3⥊0)≡≡`↕2‿3
⟨⟩≡×`⟨⟩
≡⟜(!∘0`)3‿0‿2⥊""
! % +`4
! % +`<'c'
2‿3‿5‿8‿12≡2+`↕5
! % 3‿4+`4+⌜○↕3
(2⋆1‿2‿6×⌜0‿2)≡3‿4⋆`3+⌜○↕2

# LAYER 5
(<'a')≡⊏"abc"
! % ⊏""
"a"≡⊏⥊˘"abc"
! % ⊏0‿3⥊""
(<'c')≡2⊏"abc"
! % 3⊏"abc"
! % 1.5⊏"abc"
! % 'x'⊏"abc"
(<'c')≡¯1⊏"abc"
"ccc"≡2‿¯1‿2⊏"abc"
! % ⟨⥊0,1⟩⊏≍"abc"
((3-˜↕5)⊸⊏≡2⊸⌽)↕5‿2
(0‿3⥊0)≡⟨⟩⊏2‿3⥊↕6
⟨3‿0,2‿1‿2⟩(×⟜5⊸+⌜´∘⊣≡⊏)⥊⟜(↕×´)6‿5
! % 0‿0<¨⊸⊏"abc"
(2‿0⥊0)≡⟨3‿¯1,⟨⟩⟩⊏4‿3⥊0
! % ⟨3‿¯∞,⟨⟩⟩⊏4‿3⥊0
5‿1(<⊸⊏≡⊏)↕6‿2
! % (≍≍<5‿1)⊏↕6‿2
⟨4‿0,1‿2‿3‿2‿1‿0⟩(+⌜´⊸(×⌜)≡⊏⟜(×⌜˜))+⌜˜↕5
∧´1=≡¨(<⟨⟩)(↑¨∾↓¨)⟨@,+,<@,↕3⟩
"abc"≡3↑"abce"
"e"≡¯1↑"abce"
""≡0↑"ab"
! % 2.5↑"abce"
(<⟜3⊸×↕5)≡5↑↕3
(6⥊0)≡¯6↑↕0
(≍↕3)≡1↑2‿3⥊↕6
(∾⟜0‿0‿0≡3⊸↑)2‿3⥊↕6
((0×⊏)⊸∾≡¯4⊸↑)3‿5⥊↕15
(↑⟜4≡⥊⟜0)↕3
(≍"abc")≡(<1)↑2‿3↑"abcd"
! % 2‿'c'↑"abcd"
! % (≍2‿3)↑"abcd"
(6⥊1)(↑≡⥊⟜⊑)2‿3⥊↕6
(↕¨∘↕∘(1⊸+)≡↑∘↕)5
(↑≡((↕4)≍¨2)⥊¨<)3‿2⥊"abcdef"
"d"≡3↓"abcd"
! % 0.1↓"abcd"
! % ⟨∘⟩↓"abcd"
1‿2≡⟜(¯3⊸↓)○↕4‿2
1‿1‿3‿2‿1≡≢(5⥊0)↓↕3‿2‿1
(↓∘↕≡↕∘(1⊸+)+⟜⌽↑∘↕)5
(↕3‿4)≡1↓¨⊏↕2‿3‿4
(4+⌜○↕2)≡2↕↕5
! % @↕↕5
! % 2‿1↕↕5
! % ¯1↕↕5
! % 7↕↕5
⟨⟩(↕≡⊢)4‿3⥊"abcd"
(0⊸↕≡(0≍˜1+≠)⊸⥊)↕6
(7↕6‿0⥊"")≡0‿7‿0⥊""
! % 'a'«'b'
! % "a"»'b'
! % ≍⊸»"abc"
(»˜⊸≡∧«˜⊸≡)""
"a"≡⟨⟩»"a"
⟨⟩≡"a"»⟨⟩
"aBC"≡"a"»"BCD"
"CDa"≡"a"«"BCD"
"d"≡"abcd"«⟨4⟩
((⊢⌜˜≠¨)≡(≠¨«⌜˜))""‿⟨2,3⟩‿"abcde"
"Zcab"≡"WXYZ"«´"ab"‿"c"‿""
"dab"≡'d'»"abc"
"dab"≡'d'<⊸»"abc"
(1⊸⌽≡⊏⊸«)'a'+⥊⟜(↕×´)4‿2
¯2(⌽≡↑»⊢)'a'+⥊⟜(↕×´)4‿2
6(↑≡»⟜(⥊⟜0)˜)↕4
«˜⊸≡2‿3⥊"abcdef"
(»≡0⌈-⟜1)↕6
(«≡1⊸⌽)↕6
(»≡0⌈-⟜2)⥊⟜(↕×´)5‿2
(«≡1⌽1⊸<⊸×)⥊⟜(↕×´)5‿2
! % ⌽'a'
! % ⌽<∞
≡⟜⌽⟨⟩
≡⟜⌽"a"
"ba"≡⟜⌽"ab"
(⌽≡(1-˜≠)(-○⊑∾1↓⊢)⚇1⊢)↕3‿2‿4
≡⟜⌽↕↕3
! % 2⌽'a'
! % 1‿2⌽↕4
! % ⌽‿2⌽3+⌜○↕4
! % (<<3)⌽↕4
∧´5(⌽≡⊢)¨⟨"",⥊∞,↕5,↕0‿4,2‿0‿3⥊""⟩
∧´("bcdea"≡⌽⟜"abcde")¨1+5×¯10‿¯2‿¯1‿0‿1‿6‿61
∧´⟨1,0‿2,¯1‿1‿3⟩(⊑∘⌽≡(3⊸↑)⊸⊑)⚇¯1‿∞ 2‿3‿5⥊"abcdef"
(⟨⟩⊸⌽≡<)'a'
! % /2
! % /1‿¯1‿0
! % /=⌜˜↕2
0‿4≡/1‿0‿0‿0‿1‿0
1‿1‿2≡/0‿2‿1
≡⟜/⟨⟩
! % 2/<2
! % 0‿1/"abc"
! % ⟨↕3,↕3⟩/"abc"
! % 1‿2/○≍"ab"
! % ¯1‿2/"ab"
"aabbcc"≡2/"abc"
""≡4/""
(6‿0⥊"")≡⟨5,1⟩‿⟨⟩/2‿0⥊""
3‿3‿3‿2‿2‿1≡/˜3‿2‿1
3‿3‿3‿2‿2‿1≡<⊸/3‿2‿1
(≍1∾¨1‿2‿2)≡(↕¨/↕)2‿3
(⟨⟩⊸/≡<)'a'
⟨⟩(/≡⊢)↕10
⟨⟩(/≡⊢)≍"ab"
⟨2,<3⟩(/≡⥊˜¨⟜≢/⊢)'a'+4‿2⥊↕8

# LAYER 6
! % ∾'c'
≡⟜(∾⥊¨)"abc"
(∾´≡∾)"ab"‿"cde"‿""
! % ∾"abc"
! % ∾≍"ab"‿"cde"‿""
"abc"≡∾"ab"‿'c'‿""
1‿2‿3‿4‿6‿9≡∾(⊢×≠↑↓)1+↕3
(≡⟜∾∧≡⟜(∾<))<4
⟨1‿4,⥊2⟩((∾⋆⌜⌜)≡⋆⌜○∾)⟨2‿3‿4,⟨⟩,⥊5⟩
(6‿3⥊0)≡∾⟨2‿3,3,3‿3⟩⥊¨0
! % ∾⟨2‿3,1‿3,2‿2⟩⥊¨0
"abcd"≡"abc"∾'d'
"abcd"≡"abc"∾<'d'
(↕4‿3)≡(↕3‿3)∾3∾¨↕3
(∾˜≡·¯1⊸(×´∘↓∾↑)∘≢⊸⥊≍˜)2‿3⥊"abcdef"
(∾´≡∾)⟨3‿2‿1,0‿2‿1⟩⥊¨<↕6
! % 'a'∾≍"abc"
! % "ab"∾○≍"cde"
! % (2‿3⥊↕6)∾↕2
⟨1‿2,⥊0,⥊3⟩≡⊔1‿0‿0‿2
⟨⟩≡⊔5⥊¯1
≡⟜⊔⟨⟩
! % ⊔3
! % ⊔<3
! % ⊔≍↕3
! % ⊔1.5‿0‿2
! % ⊔1‿¯2
(⊔≡⥊¨¨∘⊔∘⊑)⟨1‿0‿0‿2⟩
(≍⍟2∘<¨⌽↕3‿2)≡⊔⟨2‿1‿0,0‿1⟩
(↕0‿0)≡⊔⟨⟩‿⟨⟩
(⊔≡·≍⍟2∘<·∾⌜´/∘(0⊸=)¨)⟨0‿¯1‿0‿0,¯1‿0‿0⟩
(0‿0‿1↑⌜≍⍟2∘<∘⥊¨1‿0)≡⊔⟨2,1‿0⟩
(0‿0‿1↑⌜≍⍟2∘(<0‿0‿0⊸∾)¨1‿0)≡⊔0‿0⊸↓¨⟨2,1‿0⟩
4‿3‿2(⋈≡·(≠¨⋈∾)/⊸⊔)"abcdefghi"
⟨⟩≡(3⥊¯1)⊔"abc"
⟨⟩≡(2⥊¯1)⊔"a"
! % ⊔˜'a'‿1‿0
! % 4⊔○↕2
(≍˘1‿1‿4<∘⥊⎉1 16‿4+⌜↕4)≡2↓⟨3‿2,¯1‿0‿¯1⟩⊔2‿3‿4⥊↕24
⥊⚇0⊸≡○⊔⟜(⥊<)1‿2‿2‿¯1‿0
(∾↕¨∘≢⊸⊔)⊸≡ 3‿2‿4⥊↕24
-⟜'a'⊸(⊔≡⊔○⥊)"acc"≍"bac"
(2‿1/⟨↕0‿1,1‿1⥊3⟩)≡2⊔⥊3
((<=·↕1⊸+)≡·≢¨<¨⊸⊔⟜(<@))2‿1‿3
! % ⟨1‿2,3‿1⟩⊔2‿3⥊0
! % ⟨1‿2,3‿4‿5,6‿7⟩⊔2‿3⥊0
! % ≍⊸⊔≍˘↕3
! % ⟨⟨<3,2⟩,¯1‿0‿¯1⟩⊔2‿3‿4⥊↕24
(1‿3/⟨"a",""⟩)≡0‿¯1‿4⊔"ab"
¯1⊸↓⊸(≡○(⊔⟜"ab"))2‿3‿1
(≍1‿1‿0≍∘/⟜≍¨"bac")≡⟨0,1‿0‿3⟩⊔"ab"
(⌽˘≡·∾⟨2‿2,1‿0‿1⟩⊸⊔)"ab"≍"cd"
! % (2‿3⥊↕4)⊔↕2‿2
! % (3‿3⥊↕4)⊔↕2‿2
! % ⊐˜'a'
! % ⊏⊸⊐"abc"
! % (3‿2‿4⥊0)⊐4⥊1
2‿0‿4≡"abcd"⊐"cae"
⟨1⟩≡"abcd"⊐"b"
(<2)≡"cdef"⊐'e'
(<3)≡⊐⟜(3⊸⊏)"abcd"
(5⌊3+↕5)≡⊐⟜(3‿0‿0+⚇1⊢)↕5‿2‿1
! % ⊐+˙@
0‿0‿1‿0‿2≡⊐"ccacb"
0‿0‿1‿0‿2≡⊐≍˜˘"ccacb"
≡⟜⊐⟨⟩
! % (↕5)∊1
! % 2∊≍˘↕4
1‿0‿0‿1≡"acef"∊"adf"
(∊⟜(↕2)≡<⟜2)3⋆⌜○↕5
(<1)≡3‿4‿5∊4+⌜○↕3
! % ∊<4
('0'≠"11010001")≡∊"abacbacd"
(↑⟜1≡⟜∊⥊⟜∞)9
(⥊⟜1≡∊∘↕)6
≡⟜∊⟨⟩
≡○∊⟜(≍˜˘)"abcadbba"
! % ⍷'a'
≡⟜⍷⟨⟩
"ba"≡⍷"baa"
! % ≍⊸⍷"abc"
0‿1‿0‿0≡"abc"⍷"aabcba"
(0‿1≍0‿0)≡(1‿2≍4‿5)⍷3‿3⥊↕9
(↕3‿0)≡⍷⟜(≍˘)"abc"
'a'(=≡⍷)"abc"
(⌽¨≡⍉)↕2⥊3
(⍉≡<)'a'
∧´⍉⊸≡¨⟨<'a',"a","abc",""⟩
(↕4)(-˜⌜˜≡·⍉-⌜)↕3‿2
! % 0‿¯1‿1⍉(3⥊1)⥊1
! % 1‿0≍˘⊸⍉"ab"≍"cd"
! % 0‿2⍉+⌜˜↕3
! % 2‿0‿0⍉↕↕3
! % 3⍉↕↕3
(2×↕3)≡0‿0⍉6+⌜○↕3
(⟨⟩⊸⍉≡<)4
⟨⟩(⍉≡⊢)<4
(2‿0‿1⥊⟨⟩)≡1‿2‿0‿1⍉↕↕4
(↕1‿2‿0‿3)≡2<⊸⍉↕↕4
0⊸⍉⊸≡2‿3⥊↕6
! % ⍋'a'
! % ⍋'a'‿∘
! % ⍒2
2‿0‿3‿1‿4≡⍋"bdace"
5‿2‿4‿3‿0‿1≡⍋↓"deabb"
(⍋≡⍒)⟨"",↕0,0↑<"abc"⟩
(⍋≡↕∘≠)4‿0⥊@
(⍒≡⌽∘↕∘≠)⟨¯∞,¯1.5,π,∞,'A','a','b'⟩
(⍒≡⌽∘↕∘≠)⟨↕0,¯1.1,¯1,¯1‿¯∞,¯1‿0,¯1‿0‿0,¯1‿∞,0,6⥊0,1e¯20,1,1+1e¯15⟩
(⍒≡⌽∘↕∘≠)(<∾⟨↕0,1,1‿1,2‿1‿1,2‿1,2,1‿2,2‿2,3⟩⥊¨<)'a'
(⍋≡↕∘≠)⥊⍉(↕5)⥊⟜1⊸⥊⌜1‿'b'
(⊢≡○⍋(0‿1+≠)⥊⊢)⟨¯2,'a',1,'f'⟩
⟨1,2,3,1‿2,2‿1,1‿3,2‿2,3‿1⟩(⥊⊸(≠∘⊣∾˜¯1⊸⊑⊸(⌊∾⊣)∾×´⊸⌊)⌜≡○(⍋⥊)⥊⌜⟜(+`∘≠⟜(↕6)¨))↕4
((⥊˜-⥊⟜2‿0)∘≠≡⍋+⍒)2/↕5
! % ∧⊏⟨+⟩
! % ∧+‿-
! % ∨'c'
"edcba"≡∨"bdace"
(↕7)≡∧⍋|⟜⌽1+↕7
! % ⍋˜6
! % ⍒⟜↕4
! % (3‿2‿4⥊0)⍋4⥊1
! % (3‿2‿4⥊0)⍒1
! % ⟨+⟩⍋↕6
! % ⟨1‿3‿1,1‿3‿2⟩⍒⟨1‿3‿{𝕩}⟩
⟨1,3,∞,'e','i'⟩ (⍋≡≠∘⊣(⊣↓⊢⍋⊸⊏+`∘>)⍋∘∾) (2÷˜↕8)∾"aegz"
⟨'z','d',1‿0,0⟩ (⍒≡≠∘⊣(⊣↓⊢⍋⊸⊏+`∘>)⍒∘∾) (2÷˜↕8)∾"aegz"
(<∘⌈≡(↕6)⊸⍋)2.5
(<1)≡(↕2‿3)⍋1+↕3
(<0)≡"abc"⥊⊸⍒○<≍"acc"
! % ⊒∞
! % ⊒⊏"y"
⊒⊸≡""
("001001231"-'0')≡⊒"eccdaeccd"
(↕∘≠≡⊒˜)"abbc"+⌜↕6
(3⌊∘÷˜↕8)≡⊒8‿2⥊4÷˜↕3
! % 'c'⊒"cde"
! % (0‿4‿1‿1⥊0)⊒4‿1⥊↕4
("244031444"-'0')≡"bbac"⊒"aaabcbcbc"
(<3)≡1‿1‿1⊒0
(<0)≡(0‿4‿1⥊0)⊒4‿1⥊↕4
4(⌊⟜(↕≠)≡↑⊒⊢)7‿3⥊1‿1‿3‿1‿1
(2‿3‿0≍3‿1‿3)≡"cca"⊒○(≍˜⎉0)3↕"abcd"
