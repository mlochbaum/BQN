# String fill
' ' ≡ ⊑1↑""

# Match doesn't use fills
"ab" ≡○(0⊸↑) ↕2

# Functions that fill
"abc  "≡5↑"abc"
" ab" ≡ »"abc"
"bc " ≡ «"abc"
("abc"≍"de ") ≡ 2‿↑⥊"abcde"
(1‿3⥊4‿0‿0)   ≡ ↑‿3⥊4
(4‿3⥊12↑<¨↕10)≡ ↑‿3⥊<¨↕10

# Box and Pair generate arbitrary fill elements
∧´ (2⊸↑ ≡ 1‿0⊸×)∘<¨ ⟨π, ↕3, "", 2+⌜○↕3, ≍˘⟨2‿1,↕4‿2⟩⟩
(0¨ ≡ 1⊑2↑<) ∞‿¯∞‿0
"  "‿0 ≡ ⊑»1⥊<"xy"‿¯π
⟨7⥊0‿' '⟩ ≡ »≍<'a'⍟(2⊸|)¨↕7
∧´ (< ≡○(2⊸↑) ⋈)¨ ⟨'y', ↕0, ≍˘⟨2‿1,↕4‿2⟩⟩
((2⥊<) ≡○(3⊸↑) ⋈˜) ⟨"ab"‿⟨↕4⟩,2⟩

# Special cases
0 ≡ ⊑1↑↕0
0‿1‿2‿0 ≡ 4↑↕3
≡⟜(⊑1↑⥊∘↕) 0‿0‿0
(⥊⟜(<0⊸×) ≡ »∘↕) 1‿2
(4⊸↑ ≡ 0‿1‿2‿0⊸⊏) ↑↕2
(↑ ≡○(⊑¯7⊸↑) ↓) ≍˘⟨↕3,"ab"⟩

# Arithmetic
∧´{0‿0≡⊑»𝕏↕2‿4}¨ +‿-‿×‿÷‿⋆‿√‿⌊‿⌈‿|‿¬
∧´{(2⥊<0‿0)≡⊑»⥊⟨0‿1,23⟩𝕏○(3⥊<)⟨01,2‿3⟩}¨ +‿-‿×‿÷‿⋆‿√‿⌊‿⌈‿|‿¬‿∧‿∨
∧´{(2⥊<0‿0)≡⊑»⥊⟨0‿'c',2‿'d'⟩𝕏○(3⥊<)⟨01,"ch"⟩}¨ <‿>‿≠‿=‿≤‿≥
0‿' '‿' ' ≡ ⊑»⥊ 0‿1‿'c'+○<0‿'b'‿2
0‿' '‿0 ≡ ⊑»⥊ 0‿'b'‿'c'-○<0‿1‿'c'

# Fill-preserving functions
' ' ≡ ⊑» ⥊@
"  ab" ≡ ⥊»2‿∘⥊"abcd"
(≍3⥊<"    ") ≡ »≍3⥊<"abcd"
" cba" ≡ ¯4↑⌽"abc"
"bca " ≡ 4↑1⌽"abc"
≡○»⟜(⊏≍) ⥊<"ab"‿∞
" bc" ≡ »«"abc"
"   "‿0 ≡ ⊑1↑0⥊»4⥊<"str"‿∞
"c  " ≡ 3↑⍉'c'
(≍"   ") ≡ »⍉⌊‿1⥊"wxy"
" "≡1↑»3/"ab"
⟨⟨⟩⟩ ≡ »0‿1/↑1↓↕2
0‿0 ≡ ⊑1↑0↑0‿0⍉↕3‿2
' '‿0 ≡ ⊑»1‿2‿0⍉3↕(<'a'‿1)+↕4‿5
"cb  " ≡ 4↑2‿1⊏"abc"
(3⥊0) ≡ ⊑» ⟨2‿0,<1,≍1‿3⟩⊏↕3‿2‿4
" cba" ≡ ¯4↑⍷"cbaba"
"abc  " ≡ 5↑∧"bca"
"cabc " ≡ 5↑⊏∨3‿4⥊"abc"
0‿' ' ≡ ⊑» <` (↕3) + <0‿'x'

# Fill 0
⌽⊸≡ 7↑/2‿3
∧´{0≡⊑»𝕏"abdbcda"}¨ ≢‿⍋‿⍒‿∊‿⊐‿⊒
0≡⊑1↑≢8
∧´⟨6‿2⥊↕2,5‿2⥊↕3⟩⊸{0≡⊑1↑0⥊𝕏´𝕨}¨ ⍋‿⍒‿∊‿⍷‿⊐‿⊒

# Combinations
(' '¨⊸≍"abc") ≡ »>⟨"abc","def"⟩
"  "≡⊑1↑⥊>2⥊<0⥊<"ab"
"  "≡⊑1↑>0⥊<<"ab"
(' '¨⊸≍"abc") ≡ »"abc"≍"def"
≡○(⊑»)⟜(∾↑) (↕3) + <⟨0,'a'‿2⟩
0‿0 ≡ ⊑» 1‿3∾○↕2‿3
" ax" ≡ »"a"»"xyz"
0‿0 ≡ ⊑» 5‿4<⊸«⥊↕2‿2

# Group
"" ≡ ⊑1↑⟨⟩⊔""
0‿0 ≡ ⊑1↑⊑1↑⟨⟩⊔0⥊<↕2
0‿1‿1‿0 ≡ ⊐4↑2‿1⊔"aa"
"   " ≡ (⊑1↑0⥊⊢)¨ 2‿1⊔"aa"
