# Data
∧´{𝕩≡𝕩⁼𝕩}¨⟨¯∞,3,@,'⁼',↕4⟩
! % 3⁼4
! % 2‿3⁼2‿3.1

# Primitives
∧´ {(𝕎≡𝕎⁼)𝕩}⟜¯0.3‿0‿8¨ +‿-‿÷‿¬‿⊢‿⊣‿⌽
! % -⁼ 'a'
! % ×⁼ 5
(√√⁼)⊸≡ 0‿0.4‿π‿1e9‿∞
(⋆⁼⋆)⊸≡ ↕4
1e¯14>|1-⋆+´⋆⁼1(⊢÷«)1+↕11
∧´ {𝕩≡6𝕎6𝕎⁼𝕩}⟜1‿2‿3¨ +‿-‿×‿÷‿√‿∧‿¬‿⊢
'a' ≡ 3+⁼'d'
3 % 'd'-⁼'a'
0.3‿1.01‿π(⋆⁼⌜≡÷˜⌜○(⋆⁼))0‿2‿5.6‿∞
(⊢≡⊣⁼˜)"abcd"
! % "ab"⊣⁼"ac"
(<⁼<)¨⊸≡⟨0,⟨⟩,"abc"⟩
(/⁼/)⊸≡1‿0‿2‿4
⟨⟩≡/⁼⟨⟩
(3⊸⌽≡2⌽⁼⊢)↕5
((≢⍉⁼)≡¯1⌽≢)↕↕4
≡´⍉⍟¯1‿2⥊⟜(↕×´)2‿3‿3
2‿1(⊢≡⊣⍉⍉⁼)⥊⟜(↕×´)2‿3‿1‿4

# Self/Swap
∧´ {6(𝕎˜⁼≡𝕎⁼)𝕩}⟜¯0.8‿0‿3¨ +‿×‿∧
3.5 % +˜⁼7
0.5 % ∨˜⁼0.75
1 % +´∊⟨√,×˜⁼,∧˜⁼⟩{𝕎𝕩}⌜0‿2‿∞
∧´ -‿÷‿⋆ {3(𝕎˜⁼≡𝕏)2‿π∾⋆2}¨ +‿×‿√
4 % 16√˜⁼2
4‿2‿0(¬˜⁼≡¯1++)1‿2‿9

# Mapping and scan
! % -¨⁼ 2
! % -⌜⁼ 2
! % -˘⁼ 2
! % -˘⁼ <2
! % -`⁼ 2
! % 0-´⁼ 2
(∾˜ ≡ ·(<⌜⁼∾<¨⁼)<¨) "abcd"
"ab"‿"abc"≡1⌽⁼¨"ba"‿"bca"
(2-=⌜˜↕2)≡/˘⁼0‿1‿1≍0‿0‿1
2(⌽˘⁼≡·⍉⌽⁼⟜⍉)≍"abcde"
(4⥊1) ≡ +`⁼ 1+↕4
⟨⟩ ≡ !`⁼⟨⟩
(-⟜» ≡ +`⁼) 2|⌊×⌜˜π+↕5
(5⥊2)≡2÷`⁼2⋆-↕5
(0‿1‿1×⌜⥊˜4)≡(↕4)+`⁼3‿4⥊↕12
! % (↕3)+`⁼3‿4⥊↕12

# Composition
0.75 % (÷¬)⁼ 4
4 % ⊢∘√⁼ 2
(⌽/1‿3)≡(·+`⌽)⁼↕4
(⌽⊸/1‿3)≡(⌽·-`⌽)⁼↕4
(¯1‿0+⌜"BQN")≡(1⌽⍉)⁼3‿2⥊"PQMNAB"
-1 % (3×·√2+¬)⁼6
1 % (3⊸√+⟜7)⁼2
-1 % 5×⟜¬⁼10
9 % (√-2{𝔽})⁼1
9 % (√-2˙)⁼1
3 % 21(1+÷)⁼8
(2⊸⌽ ≡ 2⊸(-⊸⌽)⁼)"abcde"
4 % ¯2 ÷˜○¬⁼ ¯1
1e¯12>|16- 2 ÷˜○(⋆⁼)⁼ 4
(+`⌾⌽⁼≡+`⁼⌾⌽) ↕4
2 % ×˜⍟3⁼256
√⊘-{𝕗⁼≡6𝕗⁼⊢} ¯3‿2
∧´ {(𝕎≡𝕎⁼⁼)𝕩}⟜2‿4‿1¨ /‿⌽‿<

# Extensions
9 ≡ ×˜⁼⁼ ¯3
2 ≡ @ {≠𝕨‿𝕩}⁼⁼ π
0‿2‿1‿1 ≡ /⁼2‿3‿1‿1
(/2‿3) ≡ /⁼⁼2‿3‿0
(/2‿3) ≡ /⍟¯1⁼2‿3‿0
