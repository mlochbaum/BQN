#! /usr/bin/env dbqn

nc‿rt‿prim ← "-nocomp"‿"-rt"‿"-prim" ∊ •args
"Can't test runtime without the compiler!" ! ¬nc∧rt

⟨DRun,DCompile⟩ ← •Import "../dc.bqn"

GetRT ← {𝕤:
  ⟨ref⟩ ← •Import "../src/pr.bqn"
  provide ← ⟨
    •Type      # Type
    •Decompose # Decompose
    •Glyph     # Glyph
    ⊑⟨⟩⥊0⊸⥊    # GetFill
    ⋆⁼         # Log
    ≠¨⊔        # GroupLen
    ∾⊔∘⊢       # GroupOrd
    !,+,-,×,÷,⋆,⌊,=,≤,≢,⥊,⊑,↕,⌜,`,⊘
  ⟩
  •COMP provide⊸(⊣»«)⌾(1⊸⊑) DCompile ref
}

Native ← {⍎𝕩}
exec ← (rt¬nc)◶⟨Native˙, DRun˙, {𝕩⊸DRun}∘GetRT⟩ @

c ← ∾(•FLines ∾⟜"cases.bqn")¨""‿"b"
{𝕩: c ∾↩ "1 %"⊸∾¨ (0<≠)◶0‿((¬":"⊑∘∊⊢)∧'#'≠⊑)¨⊸/ •FLines "prim.bqn" }⍟⊢ prim
M ← {e‿b:
  Err ← {(@+10)∾˜""""∾b∾""": expected "∾e∾" but received "∾⍕𝕩}
  (Native e) ≢◶""‿Err Exec b
}
r ← '%' M∘((+`-2⊸×)∘=⊔⊢)¨ c
•Out ("All "∾(⍕≠c)∾" passed!")⍟(0=≠) ∾r
